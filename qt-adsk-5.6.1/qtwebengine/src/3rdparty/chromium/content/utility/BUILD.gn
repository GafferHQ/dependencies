# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//content/utility/utility.gni")
import("//media/media_options.gni")

source_set("utility") {
  # Only the public target should depend on this. All other targets (even
  # internal content ones) should depend on the public one.
  visibility = [ "//content/public/utility:utility_sources" ]

  sources =
      rebase_path(content_utility_gypi_values.utility_sources, ".", "//content")

  configs += [ "//content:content_implementation" ]

  deps = [
    "//base",
    "//content:export",
    "//content/public/child:child_sources",
    "//content/public/common:common_sources",
    "//content/public/common:mojo_bindings",
    "//courgette:courgette_lib",
    "//mojo/application/public/cpp:cpp_for_chromium",
    "//mojo/application/public/interfaces",
    "//mojo/common",
    "//mojo/shell",
    "//third_party/WebKit/public:blink_headers",
    "//third_party/mojo/src/mojo/public/cpp/bindings",
    "//url",
  ]

  if (enable_mojo_media == "utility") {
    deps += [ "//media/mojo/services:application" ]
  }
}
