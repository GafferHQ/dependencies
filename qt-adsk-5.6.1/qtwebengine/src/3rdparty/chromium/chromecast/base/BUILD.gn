# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chrome/version.gni")  # TODO layering violation!
import("//chromecast/chromecast.gni")
import("//testing/test.gni")

source_set("base") {
  sources = [
    "cast_paths.cc",
    "cast_paths.h",
    "chromecast_switches.cc",
    "chromecast_switches.h",
    "error_codes.cc",
    "error_codes.h",
    "path_utils.cc",
    "path_utils.h",
    "process_utils.cc",
    "process_utils.h",
    "serializers.cc",
    "serializers.h",
  ]

  configs += [ "//chromecast:config" ]

  public_deps = [
    "//chromecast/base/metrics",
  ]

  deps = [
    "//base",
  ]
}

test("cast_base_unittests") {
  sources = [
    "error_codes_unittest.cc",
    "path_utils_unittest.cc",
    "process_utils_unittest.cc",
    "serializers_unittest.cc",
  ]

  deps = [
    ":base",
    "//base",
    "//base/test:run_all_unittests",
    "//base/test:test_support",
    "//testing/gtest",
  ]
}

source_set("cast_sys_info") {
  sources = [
    "cast_sys_info_dummy.cc",
    "cast_sys_info_dummy.h",
    "cast_sys_info_util.h",
  ]

  if (!is_chromecast_chrome_branded && !is_android) {
    sources += [ "cast_sys_info_util_simple.cc" ]
  }

  # TODO(mbjorge): put cast_sys_info_android in here

  deps = [
    "//base",
    "//chromecast/public",
  ]
}

process_version("cast_version") {
  template_file = "version.h.in"
  output = "$target_gen_dir/version.h"
  extra_args = [
    "-e",
    "VERSION_FULL=\"%s.%s.%s.%s\"%(MAJOR,MINOR,BUILD,PATCH)",

    # TODO(slan): Populate the fields below with real values
    "-e",
    "CAST_BUILD_INCREMENTAL=20150608.181153",
    "-e",
    "CAST_BUILD_RELEASE=1.15",
    "-e",
    "CAST_IS_DEBUG_BUILD=1",
    "-e",
    "CAST_PRODUCT_TYPE=0",
  ]
}
