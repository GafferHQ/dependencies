# handle pkg-config files
isEmpty(PKG_CONFIG):PKG_CONFIG = pkg-config # keep consistent with qt_functions.prf too!

for(PKGCONFIG_LIB, $$list($$unique(PKGCONFIG))) {
    # don't proceed if the .pro asks for a package we don't have!
    !packagesExist($$PKGCONFIG_LIB):error("Package $$PKGCONFIG_LIB not found")

    PKGCONFIG_CFLAGS = $$system($$PKG_CONFIG --cflags $$PKGCONFIG_LIB)

    PKGCONFIG_INCLUDEPATH = $$find(PKGCONFIG_CFLAGS, ^-I.*)
    PKGCONFIG_INCLUDEPATH ~= s/^-I(.*)/\\1/g

    PKGCONFIG_DEFINES = $$find(PKGCONFIG_CFLAGS, ^-D.*)
    PKGCONFIG_DEFINES ~= s/^-D(.*)/\\1/g

    PKGCONFIG_CFLAGS ~= s/^-[ID].*//g

    INCLUDEPATH *= $$PKGCONFIG_INCLUDEPATH
    DEFINES *= $$PKGCONFIG_DEFINES

    QMAKE_CXXFLAGS += $$PKGCONFIG_CFLAGS
    QMAKE_CFLAGS += $$PKGCONFIG_CFLAGS
    LIBS += $$system($$PKG_CONFIG --libs $$PKGCONFIG_LIB)
}

