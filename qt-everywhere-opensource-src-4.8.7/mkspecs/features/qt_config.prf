# This file is loaded by the mkspecs, before .qmake.cache has been loaded.
# Consequently, we have to do some stunts to get values out of the cache.

exists($$_QMAKE_CACHE_) {
   qdd = $$fromfile($$_QMAKE_CACHE_, QMAKE_QT_CONFIG)
   !isEmpty(qdd): QMAKE_QT_CONFIG = $$qdd
   unset(qdd)
}
isEmpty(QMAKE_QT_CONFIG)|!exists($$QMAKE_QT_CONFIG) {
   qdd = $$QT_BUILD_TREE
   isEmpty(qdd):exists($$_QMAKE_CACHE_): qdd = $$fromfile($$_QMAKE_CACHE_, QT_BUILD_TREE)
   isEmpty(qdd): qdd = $$[QT_INSTALL_DATA]
   QMAKE_QT_CONFIG = $$qdd/mkspecs/qconfig.pri
   unset(qdd)
}
!include($$QMAKE_QT_CONFIG, "", true) {
   debug(1, "Cannot load qconfig.pri!")
} else {
   debug(1, "Loaded .qconfig.pri from ($$QMAKE_QT_CONFIG)")
   for(dir, $$list($$unique($$list($$dirname(QMAKE_QT_CONFIG) \
                                   $$split($$list($$[QMAKE_MKSPECS]), $$DIRLIST_SEPARATOR))))) {
      debug(1, "Loading modules from $${dir}")
      for(mod, $$list($$files($$dir/modules/qt_*.pri))):include($$mod)
   }
}

load(qt_functions)

