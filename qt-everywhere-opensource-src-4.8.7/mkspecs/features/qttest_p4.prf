isEmpty(TEMPLATE):TEMPLATE=app
CONFIG += qt warn_on console depend_includepath testcase

# x11 is not defined by configure (the following line is copied from gui.pro)
!win32:!embedded:!qpa:!mac:!symbian:CONFIG += x11

qtAddLibrary(QtTest)

symbian:{
    TARGET.EPOCHEAPSIZE = 0x100000 0x2000000
#    DEFINES += QTEST_NO_SPECIALIZATIONS
    TARGET.CAPABILITY="None"
    RSS_RULES ="group_name=\"QtTests\";" 
    MMP_RULES*=SMPSAFE
}

# prefix test binary with tst_
!contains(TARGET, ^tst_.*):TARGET = $$join(TARGET,,"tst_")

isEmpty(target.path) {
    target.path += $$[QT_INSTALL_PREFIX]/tests/qt4/$${TARGET}
}

INSTALLS += target
