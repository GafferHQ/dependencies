# On UNIX, we can use config tests to figure out if egl.h is in
# EGL/egl.h or GLES/egl.h. Sadly, there are no config tests on WinCE
# so we have to assume that for GLES 1.1 (CL), the EGL header is in
# GLES/egl.h. We also assume there is no separate libEGL.lib library,
# so we use the GL library instead.

wince*:contains(QT_CONFIG, opengles1) {
    INCLUDEPATH += $$QMAKE_INCDIR_OPENGL_ES1
    LIBS_PRIVATE += $$QMAKE_LIBS_OPENGL_ES1
    for(p, QMAKE_LIBDIR_OPENGL_ES1) {
        exists($$p):LIBS_PRIVATE += -L$$p
    }
    DEFINES += QT_GLES_EGL
} else {
    INCLUDEPATH += $$QMAKE_INCDIR_EGL
    LIBS_PRIVATE += $$QMAKE_LIBS_EGL
    LIBS += $$QMAKE_LFLAGS_EGL
    for(p, QMAKE_LIBDIR_EGL) {
        exists($$p):LIBS_PRIVATE += -L$$p
    }
}
