#
# qmake configuration for ios-device-clang
#
# Depends on:
#
#   QMAKE_IOS_XCODE_VERSION - set in mkspecs/common/ios/versions.conf
#

include(../../common/mac.conf)
include(../../common/gcc-base-ios.conf)
include(../../common/clang.conf)
include(../../common/ios.conf)
include(../../common/ios/versions.conf)
include(../../common/ios/clang.conf)
include(../../common/ios/qmake.conf)

# Version check
!lessThan(QMAKE_IOS_XCODE_VERSION, "4.3"): error("This mkspec requires Xcode 4.2.x or earlier")

# iOS build tools
QMAKE_CC                = /Developer/Platforms/iPhoneOS.platform/Developer/usr/bin/clang
QMAKE_CXX               = /Developer/Platforms/iPhoneOS.platform/Developer/usr/bin/clang++
QMAKE_FIX_RPATH         = /Developer/Platforms/iPhoneOS.platform/Developer/usr/bin/install_name_tool -id
QMAKE_AR                = /Developer/Platforms/iPhoneOS.platform/Developer/usr/bin/ar cq
QMAKE_RANLIB            = /Developer/Platforms/iPhoneOS.platform/Developer/usr/bin/ranlib -s
QMAKE_LINK              = $$QMAKE_CXX
QMAKE_LINK_SHLIB        = $$QMAKE_CXX

# Check that compiler is valid
!exists($$QMAKE_CXX): error("The version of Xcode installed on this system does not include the clang compiler")

load(qt_config)

# Include after config is loaded to allow autodetection on GL/ES version
include(../../common/ios/arch.conf)
