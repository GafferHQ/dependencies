#
# qmake configuration for ios-simulator-g++
#
# Depends on:
#
#   QMAKE_IOS_XCODE_VERSION - set in mkspecs/common/ios/versions.conf
#

include(../../common/mac.conf)
include(../../common/gcc-base-ios.conf)
include(../../common/g++-macx.conf)
include(../../common/ios.conf)
include(../../common/ios/versions.conf)
include(../../common/ios/arch.conf)
include(../../common/ios/g++.conf)
include(../../common/ios/qmake.conf)

# Version check (g++ was discontinued at version 4.2, but user may have added it themselves)
lessThan(QMAKE_IOS_XCODE_VERSION, "4.3"): error("This mkspec requires Xcode 4.3 or later")

# iOS build tools
QMAKE_CC                = /Applications/Xcode.app/Contents/Developer/Platforms/iPhoneSimulator.platform/Developer/usr/bin/gcc-4.2
QMAKE_CXX               = /Applications/Xcode.app/Contents/Developer/Platforms/iPhoneSimulator.platform/Developer/usr/bin/g++-4.2
QMAKE_FIX_RPATH         = /Applications/Xcode.app/Contents/Developer/Platforms/iPhoneSimulator.platform/Developer/usr/bin/install_name_tool -id
QMAKE_AR                = /Applications/Xcode.app/Contents/Developer/Platforms/iPhoneSimulator.platform/Developer/usr/bin/ar cq
QMAKE_RANLIB            = /Applications/Xcode.app/Contents/Developer/Platforms/iPhoneSimulator.platform/Developer/usr/bin/ranlib -s
QMAKE_LINK              = $$QMAKE_CXX
QMAKE_LINK_SHLIB        = $$QMAKE_CXX

# Check that compiler is valid
!exists($$QMAKE_CXX): error("The version of Xcode installed on this system does not include the g++ 4.2 compiler")

load(qt_config)
